'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	DAQ Parameters START
'
'
' WARNING! This file should be used with AlertRig scripts only!!! 
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

#include "../util/UsreyUtil.s2s"
#include "../util/DlgUtilities.s2s"


var DistanceToScreenMM$;		' Distance to screen in MM 
var DegreesPerVoltX;			' Conversion factor from ADC to visual degrees
var DegreesPerVoltY;			' Conversion factor from ADC to visual degrees
var CalibrationOffsetDegrees;   ' Calibration dots are this many degrees offset in x,y from (0,0)
var CalibrationComPort%;        ' COM port to use for calibration (connection to ASL 'Serial Out')
var CalibrationConfigFile$;     ' path to xml file for calibration config. Can be same as regular config file for eye tracker.
var EyeCoilSamplingFrequency%;		' Frequency to sample eye coil at
var NumberOfElectrodes%;		' Number of electrodes (i.e. wavemark inputs) in use
var JuicePerReward%;			' Number of juice squirts per reward
var JuiceRewardMS% := 200;      ' for Ben's new juicer this is the length of time juicer is held open.
var JuiceRewardMSMin%;          ' minimum juicer duration, used for delay-based-reward-size
var JuiceRewardMSMax%;          ' maximum juicer duration, used for delay-based-reward-size
var JuicerIndex% := 0;          ' default juicer index will not allow sampling, 1 is open high, 2 is open low
var wrongJuicerWarningGiven%;   ' Only give wrong juicer type warning once
var DisplayHardwareIndex% := 0; ' 0 for VSG/ViSaGe, 1 for Render, many scripts will not check this flag and assume VSG
var EyeXPort%;                  ' Port horizontal eye signal connected to
var EyeYPort%;                  ' Port vertical eye signal connected to
var JoystickPort%;              ' Port joystick is connected to
var EEGPort%;                   ' Port for EEG (independent of spiking electrode)
var OptoEffCopyPort%;           ' Port for optogenetic laser efference copy
var StimEffCopyPort%;           ' Port for stimulator efference copy
var PhotodiodePort%;            ' Port for a photodiode
var TemperaturePort%;           ' Port for a thermometer
var UtilityPort%;               ' Port for arbitrary utility input
var EyeXPortLabel$ := "Eye X";  ' Default label for EyeX port, hardcode almost all of these
var EyeYPortLabel$ := "Eye Y";  ' Default label for EyeY port
var JoystickPortLabel$ := "Joystk";  ' Default label for joystick port
var EEGPortLabel$ := "EEG";     ' Default label for EEG port
var OptoPortLabel$ := "OptCpy";  ' Default label for opto efference copy port
var StimPortLabel$ := "StmCpy";  ' Default label for stimulator efference copy port
var PhotodiodePortLabel$ := "hvDiod";  ' Default label for photodiode port
var TemperaturePortLabel$ := "Temptr";  ' Default label for temperature port
var UtilityPortLabel$;          ' Label for utility port, if used, leave this to be user-definable, no default
var JuicerType%;                ' Type of juicer
var pReward := 1;               ' Probability of reward
var this1401Type%;              ' Holds type of 1401
var UseRemoteStimulus% := 0;     ' if checked, send stim commands to ip.port below
var RemoteStimulusIP$ := "192.168.1.2";
var RemoteStimulusPort$ := "7000";
var SlaveStimulusIP$ := "192.168.1.2";
var SlaveStimulusPort$ := "7000";
var RecordEyeTrackerOutputFile%;  'if checked, use 1401 to start/stop eyetracke output file, send "heartbeat"
'var WavemarkPorts%[16];
'var NumWavemarkPorts%;
'var ContinuousPorts%[16];
'var NumContinuousPorts%;
GetDAQParameters();
this1401Type% := Determine1401Type%();

' Make sure Juicer information has been set, and set sequencer variable for juicer
' Quit if no juicer information has been set!
JuicerType% := GetJuicerType%();
if JuicerType% = 0 then
    JuicerCheckDialog%();
endif
SetJuicerSequencerVariable%();  'this will make the same call in various Start%() functions redundant


func JuicerCheckDialog%()
    
    GetDAQParameters();
    
    var iReturn%;
    
    var juicerList$[3];
    juicerList$[0] := "Select a juicer style";
    juicerList$[1] := "Open Hi (Ben, var dur)";
    juicerList$[2] := "Open Low (fixed dur)";
    
    'This dialog will not go away until you select one of the two juicer styles or cancel
    'but should only show up the first time that UsreyDAQ is run on a machine - after that 
    'there should be a legal value in the registry
    while JuicerIndex% < 1 or JuicerIndex% > 2 do
    
        DlgCreate("Juicer not specifed!");
        DlgList(1, "Juicer Open State:",juicerList$,3);
        
        iReturn% := DlgShow(JuicerIndex%);
        
        if iReturn% < 1 then
            halt;
        endif;
        
    wend;
        
    if iReturn% = 1 then
		SaveDAQParameters();
    endif;
        
    JuicerType% := JuicerIndex%;
        
	return iReturn%;
end



func GetJoystickPort%()
    if JoystickPort% = -1 then
        message("Joystick port has been set to -1 (off) in Config->DAQ but the current script requires joystick!  Quitting!");
        halt
    endif
    return JoystickPort%;
end

func GetEyeXPort%()
    if EyeXPort% = -1 then
        message("EyeX port has been set to -1 (off) in Config->DAQ but the current script requires eyetracking!  Quitting!");
        halt
    endif
    return EyeXPort%;
end

func GetEyeYPort%()
    if EyeYPort% = -1 then
        message("EyeY port has been set to -1 (off) in Config->DAQ but the current script requires eyetracking!  Quitting!");
        halt
    endif
    return EyeYPort%;
end

'These "which" functions are to allow us to poll if the port is set without quitting if it's not
func WhichJoystickPort%()
    return JoystickPort%;
end

func WhichEyeXPort%()
    return EyeXPort%;
end

func WhichEyeYPort%()
    return EyeYPort%;
end




func GetEEGPort%()
    return EEGPort%;
end

func GetOptoEffCopyPort%()
    return OptoEffCopyPort%;
end

func GetStimEffCopyPort%()
    return StimEffCopyPort%;
end

func GetPhotodiodePort%()
    return PhotodiodePort%;
end

func GetTemperaturePort%()
    return TemperaturePort%;
end

func GetUtilityPort%()
    return UtilityPort%;
end


'func GetDistanceToScreenMM$()
'    return DistanceToScreenMM$;
'end

func GetDegreesPerVoltX()
    return DegreesPerVoltX;
end

func GetDegreesPerVoltY()
    return DegreesPerVoltY;
end

func GetCalibrationOffsetDegrees()
    return CalibrationOffsetDegrees;
end

func GetCalibrationCOMPort%()
    return CalibrationComPort%; 
end

func GetCalibrationConfigFile$()
    return CalibrationConfigFile$;
end

func GetNumberOfElectrodes%()
    return NumberOfElectrodes%;
end

func GetJuicePerReward%()
    return JuicePerReward%;
end

func GetJuiceRewardMS%()
    return JuiceRewardMS%;
end

'When placed into the toolbar, the function must return an integer value
func DefaultReward%()
    DefaultReward();
    return 1;
end

'This is the overarching reward function that should be called to get a default reward
'Now returns the reward duration in seconds, or number of hits
func DefaultReward()
    if pReward >= Rand() then
        if JuicerType% = 1 then  'open-high juicer, dispense based on ms
            DeliverRewardMS(JuiceRewardMS%);
            var JuiceMS;  'float for calculation
            JuiceMS := JuiceRewardMS%;
            return JuiceMS/1000;
        else
            if JuicerType% = 2 then  'open-low juicer, dispense based on number of hits
                DeliverRewardNum%(JuicePerReward%);
                return JuicePerReward%;
            endif;
        endif;

    else
        LogStatus("Trial complete", "Random reward not granted!");
        return 0;
    endif;
end



'This is the function that should be called to calculate and deliver a delay-based reward
func DelayBasedReward%(hazMin,hazMax,hazActual)
    var juiceRangeMS;  'will be ms, taking out of integer
    var proportionOfHaz;
    var rewardSizeMS%;
    
    if pReward >= Rand() then
        docase    
        case JuicerType% = 1 then  'open-high juicer, dispense based on ms
            'verify hazActual does not exceed limits, because otherwise we could get bad results - like super-long juice
            docase
            case hazActual > hazMax then
                if wrongJuicerWarningGiven% = 0 then
                    message("Uh-oh! Actual hazard time greater than maximum hazard time!\nDefault reward given!");
                    wrongJuicerWarningGiven% := 1;
                endif;
                DeliverRewardMS(JuiceRewardMSMax%);
                rewardSizeMS% := JuiceRewardMSMax%;
            case hazActual < hazMin then
                if wrongJuicerWarningGiven% = 0 then
                    message("Uh-oh! Actual hazard time less than minimum hazard time!\nMinimum reward given!");
                    wrongJuicerWarningGiven% := 1;
                endif;
                DeliverRewardMS(JuiceRewardMSMin%);
                rewardSizeMS% := JuiceRewardMSMin%;
            case hazMax = hazMin then  'Henry has tried setting these values equal, and it kills things
                rewardSizeMS% := JuiceRewardMSMin%;  'just give minimum reward in this case, they will probably be the same anyway
                DeliverRewardMS(rewardSizeMS%);
            case JuiceRewardMSMax% = JuiceRewardMSMin% then  'Henry has tried to set these values equal, mathematically this should be OK , but let's force it to be OK
                rewardSizeMS% := JuiceRewardMSMin%;  
                DeliverRewardMS(rewardSizeMS%);
            else    
                proportionOfHaz := (hazActual-hazMin)/(hazMax-hazMin);
                juiceRangeMS := JuiceRewardMSMax% - JuiceRewardMSMin%;
                rewardSizeMS% := JuiceRewardMSMin% + (juiceRangeMS*proportionOfHaz);  'Note, this value will be floored to the nearest ms, not rounded.  I TELL YOU THAT IS GOOD ENOUGH!
                DeliverRewardMS(rewardSizeMS%);
            endcase
        case JuicerType% = 2 then  'open-low juicer, dispense based on number of hits
            if wrongJuicerWarningGiven% = 0 then
                message("Delay-based reward was requested, but cannot be granted\nbecause this juicer type does not have variable open duration!");
                wrongJuicerWarningGiven% := 1;
            endif;
            DeliverRewardNum%(JuicePerReward%);
            rewardSizeMS% := -1;  'return error
        endcase;
    else
        LogStatus("Trial complete", "Random reward not granted!");
        rewardSizeMS% := 0;
    endif;
    return rewardSizeMS%;
end




'This is the overarching reward function that should be called to get a specified reward
'when the input is known to be correct for the juicer type
func SpecifyReward(in%)
    if JuicerType% = 1 then  'open-high juicer, dispense based on ms
        DeliverRewardMS(in%);
    else
        if JuicerType% = 2 then  'open-low juicer, dispense based on number of hits
            DeliverRewardNum%(in%);
        endif;
    endif;
end


'This is retained to allow old functions that are not "fixed" to avoid crashes, redirect to default reward
proc DeliverReward()
    DefaultReward();
    return;
end


'This is retained to allow old functions that are not "fixed" to avoid crashes, redirect to default reward
proc DeliverRewardNumber()
    DefaultReward();
    return;
end


' Deliver a reward specified in milliseconds
proc DeliverRewardMS(iMS%)
    PrintLog("Reward %d\n", iMS%);
    SampleSeqVar(1, iMS%);
    SafeSampleKey("R");
    Yield(); YieldSystem(iMS%/1000.0);
    return;
end


func DeliverRewardNum%(iNum%)
	var i;
	for i:=1 to iNum% do Yield(.05); SafeSampleKey("J"); next
	return iNum%;
end


Proc SafeSampleKey(s$)
    var count% := 0;
    yield(.001);  'It takes the sequencer up to a full step to change the value of variable V2
    'Step is here assumed to be 1 ms, could possibly be read from sequencer?
    while SampleSeqVar(2) <> 0 and count% < 5000 do
        count% += 1;
        Yield(.001);
    wend
    SampleKey(s$);    
    return;
end



func DAQParametersDialog%()

	var iReturn%;
    var JuicerIndexDecremented%;
    var fiveInts%[5];  'There can be a maximum of 20 arguments fed to DlgShow, we need to make some space
    var nineInts%[9];  'MOAR SPACE!!!
    
    'This little bit allows us to disallow any values other than legal juicer values
    'without changing the rest of the code
    JuicerIndexDecremented% := JuicerIndex%-1;

	GetDAQParameters();
    
    var juicerList2$[2];
    juicerList2$[0] := "Open Hi (Ben, var dur)";
    juicerList2$[1] := "Open Low (fixed dur)";
    
    var displayHardwareList$[2];
    displayHardwareList$[0] := "VSG";
    displayHardwareList$[1] := "Render";
    
    'Fill fiveInts%
    fiveInts%[0] := JuicePerReward%;
    fiveInts%[1] := JuiceRewardMS%;
    fiveInts%[2] := JuiceRewardMSMin%;
    fiveInts%[3] := JuiceRewardMSMax%;
    fiveInts%[4] := JuicerIndexDecremented%;
    
    'Fill nineInts%
    nineInts%[0] := EEGPort%;
    nineInts%[1] := EyeXPort%;
    nineInts%[2] := EyeYPort%;
    nineInts%[3] := JoystickPort%;
    nineInts%[4] := OptoEffCopyPort%;
    nineInts%[5] := StimEffCopyPort%;
    nineInts%[6] := PhotodiodePort%;
    nineInts%[7] := TemperaturePort%;
    nineInts%[8] := UtilityPort%;
    
    
'	DlgCreate("DAQ parameters");
'	DlgInteger(1, "Number of Electrodes:", 1, 32, 0, 0, 1);
'	DlgReal(2, "Degrees/Volt (X):", 0, 100);
'	DlgReal(3, "Degrees/Volt (Y):", 0, 100);
'    DlgReal(4, "Calibration offset(deg)", 0.1, 100);
'    DlgInteger(5, "Calibration COM port", 1, 16);
'	DlgInteger(6, "Eye Coil Sampling Freq (Hz):", 1, 20000);
'    DlgReal(7, "Reward Prob (0-1):", 0, 1);
'	DlgInteger(8, "Juice per reward:", 0, 10, 0, 0, 1);
'    DlgInteger(9, "Juice Reward (default) ms:", 0, 1000);
'    DlgInteger(10, "Juice Reward (min) ms:", 0, 1000);
'    DlgInteger(11, "Juice Reward (max) ms:", 0, 1000);
'    DlgList(12, "Juicer Open State:",juicerList2$,2);
'    DlgInteger(13, "Eye X Port", -1, 31);
'    DlgInteger(14, "Eye Y Port", -1, 31);
'    DlgInteger(15, "Joystick Port", -1, 31);
'    DlgCheck(16, "Use Remote Stimulus");
'    DlgString(17, "Remote ip", 21);
'    DlgString(18, "Remote port", 5);
'    DlgString(19, "Slave ip", 21);
'    DlgString(20, "Slave port", 5);
'    DlgList(21, "Display Hardware:",displayHardwareList$,2);
'    DlgCheck(22, "Create Eyetracker Output File");
'    DlgAllow(0xffff, 0, DAQParametersDialogChanged%);
'	iReturn% := DlgShow(NumberOfElectrodes%, DegreesPerVoltX, DegreesPerVoltY, 
'                        CalibrationOffsetDegrees, CalibrationComPort%,
'                        EyeCoilSamplingFrequency%, pReward, fiveInts%[],
'                        EyeXPort%, EyeYPort%, JoystickPort%, 
'                        UseRemoteStimulus%, RemoteStimulusIP$, RemoteStimulusPort$, SlaveStimulusIP$, 
'                        SlaveStimulusPort$,DisplayHardwareIndex%,RecordEyeTrackerOutputFile%);
    
    DlgCreate("DAQ parameters");
    DlgList(1, "Display Hardware:",displayHardwareList$,2,22,1);
    
    DlgGroup("Used Only For Cool(er)Mouse",1,2,40,2);
	DlgInteger(2, "Number of Electrodes:",1,32,28,3,1);
    
    DlgGroup("Eyetracker",1,4,40,7);
    DlgReal(3, "Degrees/Volt (X):",0,100,27,5);
	DlgReal(4, "Degrees/Volt (Y):",0,100,27,6);
    DlgReal(5, "Calibration offset(deg)",0.1,100,27,7);
    DlgInteger(6, "Calibration COM port",1,16,28,8);
    DlgInteger(7, "Eye Coil Sampling Freq (Hz):",1,20000,28,9);
    DlgCheck(8, "Create Eyetracker Output File",2,10);
    
    DlgGroup("Reward",1,11,40,7);
    DlgReal(9, "Reward Prob (0-1):",0,1,27,12);
	DlgInteger(10, "Juice per reward:",0,10,28,13,1);
    DlgInteger(11, "Juice Reward (default) ms:",0,1000,28,14);
    DlgInteger(12, "Juice Reward (min) ms:",0,1000,28,15);
    DlgInteger(13, "Juice Reward (max) ms:",0,1000,28,16);
    DlgList(14, "Juicer Open State:",juicerList2$,2,22,17);
    
    DlgGroup("Analog Inputs, will not use if value is -1",42,1,38,11);
    DlgInteger(15,8,-1,31,71,2);  DlgText("EEG Port",43,2);
    DlgInteger(16,8,-1,31,71,3);  DlgText("Eye X Port",43,3);
    DlgInteger(17,8,-1,31,71,4);  DlgText("Eye Y Port",43,4);
    DlgInteger(18,8,-1,31,71,5);  DlgText("Joystick Port",43,5);
    DlgInteger(19,8,-1,31,71,6);  DlgText("Opto Eff Copy Port",43,6);
    DlgInteger(20,8,-1,31,71,7);  DlgText("Stim Eff Copy Port",43,7);
    DlgInteger(21,8,-1,31,71,8);  DlgText("Photodiode Port",43,8);
    DlgInteger(22,8,-1,31,71,9);  DlgText("Temperature Port",43,9);
    DlgInteger(23,8,-1,31,71,10); DlgText("Utility Port",43,10);
    DlgString(24,10,6,"",69,11);   DlgText("Utility Port Label",43,11);
    
    DlgGroup("Remote Stimulus",42,12,38,6);
    DlgCheck(25, "Use Remote Stimulus",43,13);
    DlgString(26,21,15,"",58,14);   DlgText("Remote IP",43,14);
    DlgString(27,6,5,"",73,15);   DlgText("Remote Port",43,15);
    DlgString(28,21,15,"",58,16);   DlgText("Secondary IP",43,16);
    DlgString(29,6,5,"",73,17);   DlgText("Secondary Port",43,17);
    
    DlgAllow(0xffff, 0, DAQParametersDialogChanged%);
    iReturn% := DlgShow(DisplayHardwareIndex%,NumberOfElectrodes%,DegreesPerVoltX,DegreesPerVoltY,CalibrationOffsetDegrees,
        CalibrationComPort%,EyeCoilSamplingFrequency%,RecordEyeTrackerOutputFile%,pReward,fiveInts%[],nineInts%[],
        UtilityPortLabel$,UseRemoteStimulus%,RemoteStimulusIP$,RemoteStimulusPort$,SlaveStimulusIP$,SlaveStimulusPort$);
    

	if iReturn% = 1 then
        'Unpack fiveInts%
        JuicePerReward% := fiveInts%[0];
        JuiceRewardMS% := fiveInts%[1];
        JuiceRewardMSMin% := fiveInts%[2];
        JuiceRewardMSMax% := fiveInts%[3];
        JuicerIndexDecremented% := fiveInts%[4];
        
        'Make sure juicer max open time is not less than minimum open time
        if JuiceRewardMSMax% < JuiceRewardMSMin% then
            message("Max juicer duration was less than min juicer duration,\nboth values set to max value!");
            JuiceRewardMSMin% := JuiceRewardMSMax%;
        endif;
        
        JuicerIndex% := JuicerIndexDecremented% + 1; 'Correct JuicerIndex%
        SetJuicerSequencerVariable%();  'This allows reset of juicer sequencer variable without restarting the script.  Probably redundant.
        
        'Unpack nineInts%
        EEGPort% := nineInts%[0];
        EyeXPort% := nineInts%[1];
        EyeYPort% := nineInts%[2];
        JoystickPort% := nineInts%[3];
        OptoEffCopyPort% := nineInts%[4];
        StimEffCopyPort% := nineInts%[5];
        PhotodiodePort% := nineInts%[6];
        TemperaturePort% := nineInts%[7];
        UtilityPort% := nineInts%[8];
        
		SaveDAQParameters();
	endif
	return iReturn%;
end;

func DAQParametersDialogChanged%(item%)    
    docase 
    case item% = 0 then
        DlgEnable(UseRemoteStimulus%, 26, 27, 28, 29);
        if UtilityPort% = -1 then
            DlgEnable(0,24);
        else
            DlgEnable(1,24);
        endif;
        
    case item% = 25 then
        DlgEnable(DlgValue(25), 26, 27, 28, 29);
        
    case item% = 23 then 
        if DlgValue(23) = -1 then
            DlgEnable(0,24);
        else
            DlgEnable(1,24);
        endif;
        
    endcase
    return 1;
end


	
proc GetDAQParameters()
	var key$;
    var globalKey$;
	key$ := GetRigName$() + "\\" + GetCurrentConfiguration$() + "\\DAQ";
    globalKey$ := GetRigName$() +  "\\GlobalDAQ";  'Do not use "DAQ" as "global" key, because there is old registry info in this location, will interfere with "seeding" new globals with config-specific values
    DegreesPerVoltX := GetFloatRegistryValue(key$, "DegreesPerVoltX", 0.0);
    DegreesPerVoltY := GetFloatRegistryValue(key$, "DegreesPerVoltY", 0.0);
    CalibrationOffsetDegrees := GetFloatRegistryValue(key$, "CalibrationOffsetDegrees", 8);
    CalibrationComPort% := GetIntRegistryValue%(globalKey$, "CalibrationComPort", GetIntRegistryValue%(key$, "CalibrationComPort", 2));    'We get the value from the global key, if it does not exist, the default is the value from the config-specific key, if THAT does not exist, default as default
    CalibrationConfigFile$ := GetStringRegistryValue$(key$, "CalibrationConfigFile", "NONE_SELECTED");
    EyeCoilSamplingFrequency% := GetIntRegistryValue%(key$, "EyeCoilSamplingFrequency", 1000);
    NumberOfElectrodes% := GetIntRegistryValue%(key$, "NumberOfElectrodes", 1);
    pReward := GetFloatRegistryValue(key$, "pReward", 1);
    JuicePerReward% := GetIntRegistryValue%(key$, "JuicePerReward", 1);
    JuiceRewardMSMin% := GetIntRegistryValue%(key$, "JuiceRewardMSMin", JuiceRewardMSMin%);
    JuiceRewardMSMax% := GetIntRegistryValue%(key$, "JuiceRewardMSMax", JuiceRewardMSMax%);
    JuiceRewardMS% := GetIntRegistryValue%(key$, "JuiceRewardMS", JuiceRewardMS%);
    JuicerIndex% := GetIntRegistryValue%(globalKey$, "JuicerIndex", GetIntRegistryValue%(key$, "JuicerIndex", JuicerIndex%));
    EyeXPort% := GetIntRegistryValue%(key$, "EyeXPort", 14);
    EyeYPort% := GetIntRegistryValue%(key$, "EyeYPort", 15);
    JoystickPort% := GetIntRegistryValue%(key$, "JoystickPort", 13);
    UseRemoteStimulus% := GetIntRegistryValue%(globalKey$, "UseRemoteStimulus", GetIntRegistryValue%(key$, "UseRemoteStimulus", 0));
    RemoteStimulusIP$ := GetStringRegistryValue$(globalKey$, "RemoteStimulusIP", GetStringRegistryValue$(key$, "RemoteStimulusIP", "192.168.1.2"));
    RemoteStimulusPort$ := GetStringRegistryValue$(globalKey$, "RemoteStimulusPort", GetStringRegistryValue$(key$, "RemoteStimulusPort", "7000"));
    SlaveStimulusIP$ := GetStringRegistryValue$(globalKey$, "SlaveStimulusIP", GetStringRegistryValue$(key$, "SlaveStimulusIP", "192.168.1.2"));
    SlaveStimulusPort$ := GetStringRegistryValue$(globalKey$, "SlaveStimulusPort", GetStringRegistryValue$(key$, "SlaveStimulusPort", "7000"));
    DisplayHardwareIndex% := GetIntRegistryValue%(globalKey$, "DisplayHardwareIndex", GetIntRegistryValue%(key$, "DisplayHardwareIndex", DisplayHardwareIndex%));
    RecordEyeTrackerOutputFile% := GetIntRegistryValue%(key$, "RecordEyeTrackerOutputFile", 0);
    EEGPort% := GetIntRegistryValue%(key$, "EEGPort", -1);
    OptoEffCopyPort% := GetIntRegistryValue%(key$, "OptoEffCopyPort", -1);
    StimEffCopyPort% := GetIntRegistryValue%(key$, "StimEffCopyPort", -1);
    PhotodiodePort% := GetIntRegistryValue%(key$, "PhotodiodePort", -1);
    TemperaturePort% := GetIntRegistryValue%(key$, "TemperaturePort", -1);
    UtilityPort% := GetIntRegistryValue%(key$, "UtilityPort", -1);
    UtilityPortLabel$ := GetStringRegistryValue$(key$, "UtilityPortLabel", "");
    
    GetElectrodeParameters();
end;


proc SaveDAQParameters()
    var key$;
    var globalKey$;
    'var s$;
    'var i%;
	key$ := GetRigName$() + "\\" + GetCurrentConfiguration$() + "\\DAQ";
    globalKey$ := GetRigName$() +  "\\GlobalDAQ";
    SetFloatRegistryValue(key$, "DegreesPerVoltX", DegreesPerVoltX);
	SetFloatRegistryValue(key$, "DegreesPerVoltY", DegreesPerVoltY);
    SetFloatRegistryValue(key$, "CalibrationOffsetDegrees", CalibrationOffsetDegrees);
    SetIntRegistryValue(globalKey$, "CalibrationComPort", CalibrationComPort%);
    SetStringRegistryValue(key$, "CalibrationConfigFile", CalibrationConfigFile$);
    SetFloatRegistryValue(key$, "pReward", pReward);
	SetIntRegistryValue(key$, "EyeCoilSamplingFrequency", EyeCoilSamplingFrequency%);
	SetIntRegistryValue(key$, "NumberOfElectrodes", NumberOfElectrodes%);
	SetIntRegistryValue(key$, "JuicePerReward", JuicePerReward%);
    SetIntRegistryValue(key$, "JuiceRewardMSMin", JuiceRewardMSMin%);
    SetIntRegistryValue(key$, "JuiceRewardMSMax", JuiceRewardMSMax%);
    SetIntRegistryValue(key$, "JuiceRewardMS", JuiceRewardMS%);
    SetIntRegistryValue(globalKey$, "JuicerIndex", JuicerIndex%);
    SetIntRegistryValue(key$, "EyeXPort", EyeXPort%);
    SetIntRegistryValue(key$, "EyeYPort", EyeYPort%);
    SetIntRegistryValue(key$, "JoystickPort", JoystickPort%);
    SetIntRegistryValue(globalKey$, "UseRemoteStimulus", UseRemoteStimulus%);
    SetStringRegistryValue(globalKey$, "RemoteStimulusIP", RemoteStimulusIP$);
    SetStringRegistryValue(globalKey$, "RemoteStimulusPort", RemoteStimulusPort$);
    SetStringRegistryValue(globalKey$, "SlaveStimulusIP", SlaveStimulusIP$);
    SetStringRegistryValue(globalKey$, "SlaveStimulusPort", SlaveStimulusPort$);
    SetIntRegistryValue(globalKey$, "DisplayHardwareIndex", DisplayHardwareIndex%);
    SetIntRegistryValue(key$, "RecordEyeTrackerOutputFile", RecordEyeTrackerOutputFile%);
    SetIntRegistryValue(key$, "EEGPort", EEGPort%);
    SetIntRegistryValue(key$, "OptoEffCopyPort", OptoEffCopyPort%);
    SetIntRegistryValue(key$, "StimEffCopyPort", StimEffCopyPort%);
    SetIntRegistryValue(key$, "PhotodiodePort", PhotodiodePort%);
    SetIntRegistryValue(key$, "TemperaturePort", TemperaturePort%);
    SetIntRegistryValue(key$, "UtilityPort", UtilityPort%);
    SetStringRegistryValue(key$, "UtilityPortLabel", UtilityPortLabel$);
    
    SaveElectrodeParameters();
end;

func GetUseRemoteStimulus%()
    return UseRemoteStimulus%;
end

func GetRemoteStimulusIP$()
    return RemoteStimulusIP$;
end

func GetRemoteStimulusPort$()
    return RemoteStimulusPort$;
end

func GetSlaveStimulusIP$()
    return SlaveStimulusIP$;
end

func GetSlaveStimulusPort$()
    return SlaveStimulusPort$;
end


func GetJuicerType%()
    
    'Get the proper value for the Juicer Index
    var key$;
    key$ := GetRigName$() + "\\" + GetCurrentConfiguration$() + "\\DAQ";
    JuicerIndex% := GetIntRegistryValue%(key$, "JuicerIndex", JuicerIndex%);
    
    return JuicerIndex%;
    
end



func SetJuicerSequencerVariable%()
   
    'Set sample sequencer variable 3 to 0 if juicer is open-high, and to 1 if juicer is open-low
    'This is equivalent to setting sample sequencer variable 3 to the value of the bit when closed
    if JuicerIndex% = 1 then 'open high
        SampleSeqVar(3,0);
    else
        if JuicerIndex% = 2 then 'open low
            SampleSeqVar(3,1);
        endif
    endif
    
end



func Determine1401Type%()
    'This function determines the type of 1401 connected and returns the appropriate value
    'to be given to SampleOptimise().  If there is no 1401 connected, it will default to 
    'Power1401-3.  If type is original Power1401, will default to 400-MHz type.
    var h%;
    var resp$;
    
    h% := SampleStatus();
    if h% >= 0 then  'if sampling is set up or started, do not open connection to 1401.  Calling the function at this point is an error, but should not be fatal
        message("Get1401Type%%() was called while sampling was already setup or occurring!\nBy design this should not happen.  Contact Jeff.\nHowever, this error should not be fatal and sampling can continue.");
        return 78;  'This will indicate an error - if we somehow subsequently call SampleOptimise (we shouldn't), kill things, this is a serious problem
    endif  
    
    h% := U1401Open();  'opens a connection to the 1401, key here is that the return indicates the type of 1401
    if h% < 0 then 'this is an error code, there is no 1401 detected
        return 77;  'this value will indicate no 1401 at sample optimise step
        'No connection close necessary
    else
        if h% = 3 then  'if unit is power1401, get type of ADC as well
            U1401Write("INFO,S,20;");
            U1401Read(resp$);  'resp will be "4" for 400 kHz and "6" for 625 kHz (value is conincidental, does not represent 100s of kHz)
            if val(resp$) = 6 then
                h% := 3625;  'this value will indicate the 625 kHz Power1401
            endif;
        endif
        U1401Close();  'Close connection to 1401 to allow sampling
        return h%;
    endif

end


func Get1401Type%()
    'This function should be called inside a SampleOptimise call to convert the 1401 type
    'as returned by U1401Open to the correct type for the SampleOptimise call.  Too bad these
    'types don't line up, or this function would be unnecessary.
    var h%;
    docase
    case this1401Type% = 0 then
        return 0;  'minimal 1401 (pre-Power1401, we do not have these)
    case this1401Type% = 1 then
        return 3;  '1401plus (we do not have these)
    case this1401Type% = 2 then
        return 6;  'micro1401 mkI 
    case this1401Type% = 3 then
        return 2;  'power1401, 400 MHz  
    case this1401Type% = 3625 then
        return 5;  'power1401, 625 MHz     
    case this1401Type% = 4 then
        return 4;  'micro1401 mkII   
    case this1401Type% = 5 then
        return 7;  'power1401 mkII 
    case this1401Type% = 6 then
        return 8;  'micro1401-3
    case this1401Type% = 7 then
        return 9;  'power1401-3
    case this1401Type% = 77 then
        message("No 1401 detected! Sample optimization set for Power1401-3!");
        return 9;  'power1401-3  
    case this1401Type% = 78 then  'this really shouldn't happen
        message("Bad order of sampling setup/sample optimization calls.  Quitting!");
        halt
    endcase
    
end


'proc GetElectrodeParameters()
'	var key$;
'    var s$;
'	key$ := GetRigName$() + "\\" + GetCurrentConfiguration$() + "\\DAQ";
'    s$ := GetStringRegistryValue$(key$, "WavemarkPorts", "");
'    NumWavemarkPorts% := ReadStr(s$, WavemarkPorts%[]);
'    s$ := GetStringRegistryValue$(key$, "ContinuousPorts", "");
'    NumContinuousPorts% := ReadStr(s$, ContinuousPorts%[]);
'end;
'
'proc SaveElectrodeParameters()
'    var key$;
'    var s$;
'    var i%;
'	key$ := GetRigName$() + "\\" + GetCurrentConfiguration$() + "\\DAQ";
'    SetStringRegistryValue(key$, "WavemarkPorts", GetWavemarkPorts$());
'    SetStringRegistryValue(key$, "ContinuousPorts", GetContinuousPorts$());    
'end;


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	DAQ Parameters END
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
