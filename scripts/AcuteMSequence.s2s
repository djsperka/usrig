' $Id$
'
' AcuteMSequence.s2s
'
' This is based on the original m-sequence script NewMSequence.s2s. It has been 
' updated to eliminate hard-coded path names and other slicker more portable
' features of Spike2. To the user there is very little difference. 

#include "../util/UsreyUtil.s2s"
#include "../util/FileUtilities.s2s"
#include "UsreyDAQ.s2s"

var amsFramesPerTerm%;
var amsDotSize%;
var amsXOrigin%, amsYOrigin%;
var amsRepeats% := 1;
var amsColorIndex%;
var amsColor$[4];
var amsDoNeuropixel%;
var neuropixelStatus%;
var autoNameStr$;

' set to 1 if using visage
var amsUseMEAMSeq% := 1;

' ports used for recording
const amsNPorts% := 8;   ' Number of analog ports
var amsWavemarkPorts%[amsNPorts%]; 
var amsContinuousPorts%[amsNPorts%];
var amsNWavemarks% := 0;
var amsNContinuous% := 0;

' ============================================================================ '
' Serial Port constants
' ============================================================================ '
const serialCOMPort% := 8; 
const serialBaudRate% := 115200;
const serialDataBits% := 8;
const serialParity% := 0;
const serialStopBits% := 1;
const serialFlowCtrl% := 0;


' daq channels for triggers from vsg
var amsChanTrigger% := 21;
var amsChanVSGReady% := 22;
var amsChanStim%    := 23;

' prog handles
var amsHandleMsgSvr := -1;
var amsHandleStim := -1;

' window handles
var amsWindowData%;

' states
const stateStartup%             :=  1;
const stateVSGWait%             :=  2;
const stateVSGConfirm%          :=  3;
const stateStartStim%           :=  4;
const stateStim%                :=  5;
const stateDone%                :=  6;
const stateSendNeuropixelStart% :=  7;

var iState% := stateStartup%;              ' This holds the current state

' stuff left over from original script. 
var index;
var TriggerTime:=-1,NextTriggerTime, BlockData$[8],BlockIndex;
var FirstTriggerTime := -1;
var SpikeCount;
var TriggerNumber:=0;
var TimeLeft,PercentComplete;







for index := 1 to 8 do 
	BlockData$[index-1]:=str$(index);
next

amsColor$[0] := "Black and white";
amsColor$[1] := "S cone isolating";
amsColor$[2] := "M cone isolating";
amsColor$[3] := "L cone isolating";

View(App(0)).Window(0,0,100,95);




'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Script starts here

LogInit(1);

if AcuteMSequenceDialog%() = 0 then
    halt;
endif

' Sampling config
CreateSampleConfiguration();

' Prepare display windows, input channels, etc. 
InitializeWindows();


	
ToolbarSet(1,"Quit",Quit%);
ToolbarSet(2,"Sample start",Start%);
ToolbarSet(3,"Sample stop", Stop%);
ToolbarEnable(3,0);
Toolbar("Go",1234);



func AcuteMSequenceDialog%()
    var status%;
    var tmp%,AdjustedY%;
    
    GetAcuteMSequenceParameters();
    
    DlgCreate("M Sequence Parameters");
    DlgInteger(1, "Frames per term: ", 1, 1000000);
    DlgInteger(2, "Dot size: ", 4, 24, 0, 0, 4);
    DlgInteger(3, "Center X coord: ", -400, 400);
    DlgInteger(4, "Center Y coord: ", -300, 300);
    DlgInteger(5, "Number of cycles: ", 1, 100);
    DlgList(6, "Color properties: ", amsColor$[]);
    DlgCheck(7,"Do Neuropixel Recording",2,7);
    status% := DlgShow(amsFramesPerTerm%, amsDotSize%, amsXOrigin%, amsYOrigin%, amsRepeats%, amsColorIndex%, amsDoNeuropixel%);
    
    if status% = 1 then
        
        ' dot size and y coord must be multiples of 4.
        tmp% := ((amsDotSize% + 2)/4)*4;
        if tmp% <> amsDotSize% then
            Message("Dot size must be multple of 4: using "+str$(tmp%)+" instead.");
            amsDotSize% := tmp%;
        endif
        
        tmp% := ((amsYOrigin% + 2)/4)*4;
        if tmp% <> amsYOrigin% then
            Message("Center Y coordinate must be multple of 4: using "+str$(tmp%)+" instead."); 
            amsYOrigin% := tmp%;
        endif
        
        SaveAcuteMSequenceParameters();
        
    endif
        
    return status%;
end


proc GetAcuteMSequenceParameters()
	var key$;
	key$:=GetRegistryKey$(1) + "\\Scripts\\MSequence";
    amsFramesPerTerm% := GetIntRegistryValue%(key$, "FramesPerTerm", amsFramesPerTerm%);
    amsDotSize% := GetIntRegistryValue%(key$, "DotSize", amsDotSize%);
    amsXOrigin% := GetIntRegistryValue%(key$, "XOrigin", amsXOrigin%);
    amsYOrigin% := GetIntRegistryValue%(key$, "YOrigin", amsYOrigin%);
    amsRepeats% := GetIntRegistryValue%(key$, "Repeats", amsRepeats%);
    amsColorIndex% := GetIntRegistryValue%(key$, "ColorIndex", amsColorIndex%);
    amsDoNeuropixel% := GetIntRegistryValue%(key$, "DoNeuropixel", amsDoNeuropixel%);
    return;
end

proc SaveAcuteMSequenceParameters()
	var key$;
	key$:=GetRegistryKey$(1) + "\\Scripts\\MSequence";
    SetIntRegistryValue(key$, "FramesPerTerm", amsFramesPerTerm%);
    SetIntRegistryValue(key$, "DotSize", amsDotSize%);
    SetIntRegistryValue(key$, "XOrigin", amsXOrigin%);
    SetIntRegistryValue(key$, "YOrigin", amsYOrigin%);
    SetIntRegistryValue(key$, "Repeats", amsRepeats%);
    SetIntRegistryValue(key$, "ColorIndex", amsColorIndex%);
    SetIntRegistryValue(key$, "DoNeuropixel", amsDoNeuropixel%);
    return;
end


proc CreateSampleConfiguration()
    
    var i%;
    
	SampleClear(); 'Set standard sampling state
	SampleAutoComment(0); 'Prompt for comment after sampling
	SampleAutoCommit(60); 'Seconds between each file commit
    if amsDoNeuropixel% = 1 then
        SampleBigFile(0);  'Create an .smr file INSTEAD of .smrx so that Scottie's analysis suite can read it, max file size is 2GB
        'SampleBigFile(1);  'Create a "big" .smr file, max file size 1 TB.  It is not known if Scottie's analysis suite can read these.
    endif;
    
    FilePathSet(GetDataPathRoot$() + "\\" + GetDataSetName$(), 3);
    FilePathSet(GetDataPathRoot$() + "\\" + GetDataSetName$(), 1);
    SampleAutoName$(dataCellName$ + "_mseq_000");
	SampleAutoFile(1);

    ' Get wavemark/continuous ports
    amsNWavemarks% := GetWavemarkPorts%(amsWavemarkPorts%[]);
    amsNContinuous% := GetContinuousPorts%(amsContinuousPorts%[]);
    PrintLog("wm %d cont %d\n", amsNWavemarks%, amsNContinuous%);
    
    'Wavemark channel recording definitions
'    for i% := 0 to amsNWavemarks%-1 do
'		SampleWaveMark(i% + 1, amsWavemarkPorts%[i%], 2000, 34, 15, 28000); ' chan, port, event rate, points, pre-trigger, rate
'		SampleTitle$(i% + 1, "WMrk " + str$(amsWavemarkPorts%[i%])); 
'	next
'    for i% := 0 to amsNContinuous%-1 do
'        SampleWaveform(amsNWavemarks% + i% + 1, amsContinuousPorts%[i%], 20000);
'        SampleTitle$(amsNWavemarks% + i% + 1, "Cont " + str$(amsContinuousPorts%[i%]));
'    next
    

    ' Changed!  To avoid confusion wavemarks now are place on channels (Port+1) and waveforms on channels (Port+8)
    for i% := 0 to amsNWavemarks%-1 do
        SampleWaveMark(amsWavemarkPorts%[i%]+1, amsWavemarkPorts%[i%], 2000, 34, 15, 28000); ' chan, port, event rate, points, pre-trigger, rate
		SampleTitle$(amsWavemarkPorts%[i%]+1, "WMrk " + str$(amsWavemarkPorts%[i%]));
        if amsWavemarkPorts%[i%] > 7 then
            message("The rig should only be set up to collect data from Ports 0-7!  Quitting!");
            'This message comes up because of the non-collapsed (i.e. non-continuous) channel numbers.  There is a "hack" a few lines below
            'which sets the continuous waveforms to live on channels numbered 7 greater than the corresponding WaveMark channel.  We can get
            'away with this because the Thomas system which feeds into the CED has only 7 electrode channels which are fed to Ports 0-6 on
            'the CED.  As of now, the channels that can legally hold WaveMarks are channels 1-7 and the channels that can legally hold
            'continuous waveforms are channels 8-14.  Since channel 19 holds pulses, there isn't a lot more room to work under this scheme
            'before needing to reposition other channels anyway (this non-collapsed scheme would work up to a hard-coded 9 rather than 7 Ports).
            'So if you need to use Port 7/8, you'll need to change the hardcode here and for tunNContinuous% below to 8/9.  If you need
            'to go beyond Port 8, you'll have to either move some of the hardcoded pulse channels, or dump the non-collapsed numbering scheme. -jj
            halt;
        endif;
	next
    for i% := 0 to amsNContinuous%-1 do
        SampleWaveform(amsContinuousPorts%[i%]+9, amsContinuousPorts%[i%], 20000);  'this hack only works because wavemark chans are limited to channels 1-7
        SampleTitle$(amsContinuousPorts%[i%]+9, "Cont " + str$(amsContinuousPorts%[i%]));
    next
    

    ' djs 10-19-2011 Change this to port 0 (and check the "Event Ports 1+2..." box in preferences/Sampling....
    SampleEvent(amsChanTrigger%, 0, 1, 1000); 
    SampleTitle$(amsChanTrigger%, "Terms");
    
   	SampleEvent(amsChanVSGReady%, 1, 2, 1000); 'Trigger channel, level
	SampleComment$(amsChanVSGReady%, "VSG Rdy");
	SampleTitle$(amsChanVSGReady%, "VSG Rdy");
    
   	SampleEvent(amsChanStim%, 3, 2, 1000); 'Trigger channel, level
	SampleComment$(amsChanStim%, "Stim");
	SampleTitle$(amsChanStim%, "Stim");
    
	SampleSequencer(script$ + "Tuning.pls");
    SampleOptimise(2, 2, 2, 2, 50);  'Set optimise mode (do this last)
    SampleMode(1); 'Continuous sampling
    
end;


proc InitializeWindows()
    var i%;
    var AutoCorr%;
    
	View(App(8)).WindowVisible(0);	'Hide the standard sample bar
    
    amsWindowData% := FileNew(0,4);
    View(amsWindowData%).WindowVisible(1);
    View(amsWindowData%).Window(0,50,100,100);
    
'Henry wants Autocorr windows gone, they are automatically processed, shouldn't show up anywhere else, I hope.   
'    if amsNWavemarks% > 0 then
'        for i% :=1 to amsNWavemarks% do
'            AutoCorr% :=View(amsWindowData%).SetEvtCrl(amsWavemarkPorts%[i%-1]+1, 60, .0005, .015, amsWavemarkPorts%[i%-1]+1);
'            View(AutoCorr%).ProcessAuto(5, 0, 1, 5);
'            View(AutoCorr%).Window((i%-1)*(100/amsNWavemarks%), 0, i%*(100/amsNWavemarks%), 33);
'            View(AutoCorr%).WindowVisible(1);
'            View(AutoCorr%).WindowTitle$("Channel "+str$(amsWavemarkPorts%[i%-1]+1)+" AutoCorr");
'        next
'    endif
    
    SaveParameterFile();
end

proc SaveParameterFile()
    var handle;
    var filename$;
    
    filename$ := ReplaceFileExtension$(-1, "par");
    handle := FileOpen(filename$, 8, 1);
    if handle <0 then 
        Message("Could not create parameter file " + filename$);
    else
        LogInfo("Writing parameters to file " + filename$);
        Print("[M Sequence]\n");
        Print("\"Number Of Channels\" %d\n",amsNWavemarks%);
        Print("\"Frames Per Term\" %d\n",amsFramesPerTerm%);
        Print("\"Dot Size\" %f\n",amsDotSize%);
        Print("\"X Origin\" %f\n",amsXOrigin%);
        Print("\"Y Origin\" %f\n",amsYOrigin%);
        Print("\"Number of Cycles\" %d\n",amsRepeats%);
        Print("\"Screen Distance\" %s\n", GetDistanceToScreenMM$());
        FileClose();
    endif
    
    return;
end



''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func Quit%()
    ProgKill(amsHandleMsgSvr); 
    return 0;
end;

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func Start%()
    var cmd$;
    var tmp$;
    var i%;
    
    ' Start message server
    amsHandleMsgSvr:=ProgRun(GetBinDir$(1) + "MsgSvr.exe", 0);
    Yield();    ' Make sure server can start
    
    ' If the message server was already running (because of some failure or error) it will still have messages in it. We must
    ' clear those out by sending a special message "clear" via SendMessage. 
    ProgRun(GetBinDir$(1) + "SendMessage clear");
    
    
    'For serial communications with Neuropixel, use the same values but do not send initial message
    if amsDoNeuropixel% =  1 then
        SerialOpen(serialCOMPort%, serialBaudRate%, serialDataBits%, serialParity%, serialStopBits%, serialFlowCtrl%);
    endif;    
    
    
    ' now as user to start matlab processing. 
    Message("Start MultiRealTimeM(" + str$(amsNWavemarks%) + ") and press OK to start");
    
    ' Start sampling
    SampleStart(0);
    View(amsWindowData%);    
    
    if GetUseRemoteStimulus%() = 0 then
        if amsUseMEAMSeq% = 0 then
            cmd$ := GetBinDir$(1) + "msequence.exe -t " + str$(amsFramesPerTerm%) + " -d " + str$(amsDotSize%) + 
            " -p " + str$(amsXOrigin%) + "," + str$(-amsYOrigin%) + " -R " + str$(amsRepeats%) + " -f " + GetStimDir$(1) + "MSequenceTerms";
        else
            cmd$ := GetBinDir$(1) + "meamseq.exe -t " + str$(amsFramesPerTerm%) + " -d " + str$(amsDotSize%) + 
            " -p " + str$(amsXOrigin%) + "," + str$(-amsYOrigin%) + " -R " + str$(amsRepeats%) + " -m " + GetStimDir$(1) + "MSequenceTerms" + " -o 15 -r 16 -c 16 -T";
        endif
    else
        cmd$ := GetBinDir$(1) + "remote.exe " + GetRemoteStimulusIP$() + " " + GetRemoteStimulusPort$() + " msequence " + 
        " -r 16 -c 16 -t " + str$(amsFramesPerTerm%) + " -d " + str$(amsDotSize%) + 
        " -p " + str$(amsXOrigin%) + "," + str$(-amsYOrigin%) + " -R " + str$(amsRepeats%)
    endif
    
    LogInfo(cmd$);
    amsHandleStim := ProgRun("cmd /c " + cmd$);
    
    ToolbarEnable(3,1);
    ToolbarEnable(2,0);
    ToolbarEnable(1,0); 
    ToolbarSet(0, "", Idle%);
    
    ' djs. Modify StartM message to also include the Spike2 channel numbers. 
    ' they will be appended to the msg like this: StartM 1 3 7
    ' It should be possible to eliminate the need to run MultiRealTimeM with an argument (number of channels), 
    ' since the StartM will effectively have that info. 
    tmp$ := "";
    if amsNWavemarks% > 0 then
        for i% := 0 to amsNWavemarks%-1 do
            tmp$ += str$(amsWavemarkPorts%[i%]+1) + " ";
        next
    endif
    
    LogInfo("Append channel numbers to StartM: " + tmp$);
    ProgRun(GetBinDir$(1) + "SendMessage StartM " + tmp$);
    
    'wait 5 seconds for some spikes to come in, and then optimize the display for them.
    Yield(5);
    View(amsWindowData%).Optimise(-1, 0, MaxTime());
    return 1;
end;

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func Stop%()
    var serialStatus%;
    
    ' remove the idle process. 
    ToolbarSet(0, "");
    ToolbarEnable(1, 1);
    ToolbarEnable(3, 0);
    
    ' send the quit command before stopping sampling
    SampleKey("Q");
    
    docase
    case amsDoNeuropixel% = 1 and neuropixelStatus% = 1 then 
        serialStatus% := SendSerialMsgNeuropixel(serialCOMPort%, "STOP", "\n");
        SerialClose(serialCOMPort%);
        if serialStatus% = 0 then
            message("Warning! SpikeGLX recording may not have stopped!\nPlease manually stop SpikeGLX!");
        endif;
    case amsDoNeuropixel% = 1 and neuropixelStatus% = 0 then 'On failure to start recording, neuropixelStatus% will be 0, don't try stopping recording that didn't start
        SerialClose(serialCOMPort%);
    endcase;
    
    
    while ProgStatus(amsHandleStim) > 0 do
        Yield(1);
    wend
    SampleStop();
    
    ' better let matlab know we've stopped
    ProgRun(GetBinDir$(1) + "SendMessage StopM");
    Yield(1);

    return 1;
end; 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


var tTrigger := -1;
var tLast := -1;
var tNow := -1;
var tLastTrigger := -1;
var vTrigger;
var tStateStart := -1;

func Idle%()
    var status% := 1;
    
	tNow := View(amsWindowData%).MaxTime();
    
    docase 
    case iState% = stateStartup% then
        
        LogStatus("stateStartup", "Starting up.");
        ChangeStateTo(stateVSGWait%, tNow);
        
    case iState% = stateVSGWait% then
        
        ' Waiting for VSG to indicate it is ready. The indication is a pulse (up, then down, 1 frame apart) on port 6.
        tTrigger := View(amsWindowData%).NextTime(amsChanVSGReady%, tLast, vTrigger);
        if tTrigger > tLastTrigger and vTrigger = 0 then
            LogStatus("stateVSGWait", "Got leading edge of trigger.");
            tLastTrigger := tTrigger;
            ChangeStateTo(stateVSGConfirm%, tTrigger);                
        endif;
            
    case iState% = stateVSGConfirm% then
            
        ' Waiting for VSG to confirm it is ready. The downward side of the pulse is expected...
        tTrigger := View(amsWindowData%).NextTime(amsChanVSGReady%, tLastTrigger, vTrigger);
        if tTrigger > tLastTrigger and vTrigger = 1 then                
            LogStatus("stateVSGConfirm", "Got trailing edge of trigger.");
            tLastTrigger := tTrigger;
            if amsDoNeuropixel% = 0 then
                ChangeStateTo(stateStartStim%, tTrigger);
            else
                ChangeStateTo(stateSendNeuropixelStart%, tTrigger);
            endif;
        endif;
        
    case iState% = stateSendNeuropixelStart% then

        autoNameStr$ := dataDataSet$ + "_" + FileName$(-4);
        printlog("Sending START to Neuropixel, using name string '%s'\n",autoNameStr$);
        
        neuropixelStatus% := SendSerialMsgNeuropixel(serialCOMPort%, "START " + autoNameStr$, "\n");
        if neuropixelStatus% = 0 then
            message("SpikeGLX recording did not successfully start!  Quitting!"); 
            Stop%();
        endif;
        
        ChangeStateTo(stateStartStim%,View(amsWindowData%).MaxTime());  'If implemented, put in a real time for tStateStart
            
    case iState% = stateStartStim% then
        
        ' Issue sample key to start stim, then switch to looking for triggers
        LogStatus("stateStartStim", "Starting stimulus.");
        Yield(1.0);
        SampleKey("S");
        ChangeStateTo(stateStim%, tNow);
        
    case iState% = stateStim% then
        
'        if CountSpikes%() < 0 then
        if CountSpikesWithDelay%(0) < 0 then
            ChangeStateTo(stateDone%, tNow);
        endif
        
    case iState% = stateDone% then
        
        Stop%();
        
    else
        
        LogError("", "Unknown state " + str$(iState%));
        Stop%();
        status% := 0;
        
    endcase
    
    return status%;
end

proc ChangeStateTo(stateTo%, tStart)
	iState% := stateTo%;
	if tStart > 0 then tStateStart := tStart; endif;
end;




func CountSpikes%()

    var status% := 1;   ' will be set to -1 when all triggers have been counted
    
    ' TriggerTime is the last trigger found. On starting the msequence TriggerTime is initialized to -1.
    ' NextTriggerTime is the first trigger found after TriggerTime. When both NextTriggerTime and TriggerTime
    ' are valid (neither is <0) they consitute the full period of a single term of the m-sequence.
    ' 
    ' Note that the line below was from the original msequence code -- in that script sampling started on the first 
    ' trigger, and that trigger is NOT in the data file. The first term of the m-sequence starts at time 0, however, 
    ' so the value of TriggerTime is set to 0 before starting. This script starts sampling prior to the onset of the
    ' first term of the msequence, so the first trigger found in the file must be saved; we do not count spikes until
    ' we find the second trigger. When multiple repeats are run there is further confusion. The modulo division 
    ' takes care of all: Whenever the number of triggers counted (TriggerNumber) is a multiple of 32768 we have to 
    ' save one trigger before counting spikes in trigger intervals. 
'    if TriggerTime=-1 then TriggerTime:=0; endif    
    
    NextTriggerTime:=View(amsWindowData%).NextTime(amsChanTrigger%,TriggerTime);
    if NextTriggerTime > 0 then
        '        if TriggerTime < 0 then
        if TriggerNumber mod 1000 = 0 then
            PrintLog("next %f now %f delta %f\n", NextTriggerTime, MaxTime(), MaxTime()-NextTriggerTime);
        endif
        
        if TriggerNumber mod 32768 = 0 then
            FirstTriggerTime := NextTriggerTime;
            TriggerTime := NextTriggerTime;
            TriggerNumber := TriggerNumber + 1;
        else
            ' TriggerTime is valid, so the interval [TriggerTime, NextTriggerTime] is a term in the sequence. 
            ' Count spikes for each data channel and save it in BlockData$[]
            for index := 1 to amsNWavemarks% do 
                'SpikeCount:=View(amsWindowData%).count(index,TriggerTime,NextTriggerTime);
                SpikeCount:=View(amsWindowData%).count(amsWavemarkPorts%[index-1]+1,TriggerTime,NextTriggerTime);
                BlockData$[index-1]:=BlockData$[index-1]+" "+str$(SpikeCount);
            next 
            TriggerTime:=NextTriggerTime;
            BlockIndex:=BlockIndex+1;
            TriggerNumber:=TriggerNumber+1;
            
            ' Was that the last trigger? 
            if TriggerNumber = 32768 * amsRepeats% then
                status% := -1;
            endif
            
            ' BlockIndex counts the number of terms we've collected and stored in BlockData$[] for each channel. 
            ' The test below seems to be a rough way of balancing computing time against the rate at which terms
            ' pile up. Not too sure about that, though. Regardless, the test determines when we push the BlockData$[]
            ' up to the MsgSvr.
            
            if BlockIndex*amsFramesPerTerm%>=(40*amsNWavemarks%) or status% < 0 then 
                for index := 1 to amsNWavemarks% do 
                    SendMessage(BlockData$[index-1]);
                    BlockData$[index-1]:=str$(index);
                next
                PercentComplete:=TriggerNumber/(32768*amsRepeats%);  'This line and the following line removed from
                TimeLeft:=(TriggerTime/PercentComplete)-TriggerTime;  'FOR loop just above because they only need to be done once -jj
                ToolbarText("M Sequence "+str$(100*PercentComplete)+"% complete. Estimated "+str$(TimeLeft)+" seconds left");
                BlockIndex:=0;
            endif
            
        endif
    endif
    
    return status%;
end


func CountSpikesWithDelay%(msFromLastCheck%)

    var status% := 1;   ' will be set to -1 when all triggers have been counted
    
    ' TriggerTime is the last trigger time we have processed. 
    ' The 'processing' here is to search for triggers up to the current time (the previous version
    ' looked for the next trigger only - leads to pile-up of unprocessed triggers on current hardware). 
    ' If msFromLastCheck% is greater than zero, then we only process if (MaxTime() - TriggerTime) is greater 
    ' than msFromLastCheck%. 
    '
    ' Make sure that these arrays are large enough.
    
    var triggers[1000];
    var spikes[1000];    
    var ntrig%;
    var nspikes%;
    var tNow;
    var tLastTrigger;
    var tTempTrigger;
    var ichannel%;
    var itrig%;
    
    tNow := MaxTime();    
    if msFromLastCheck% <= 0 or TriggerTime < 0 or tNow-TriggerTime>msFromLastCheck% then
        
        ' Get whatever triggers there are to be found. 
        ' Since ChanData will return events that occur AT the initial time, I add a small 
        ' amount to it so the trigger is not found again. 
        ntrig% := ChanData(amsChanTrigger%, triggers, TriggerTime+.001, tNow);
        if ntrig% > 0 then
            
            if TriggerNumber mod 32768 = 0 then
                
                TriggerTime := triggers[0];
                TriggerNumber := TriggerNumber + 1;
                PrintLog("Initial trigger at %f\n", TriggerTime);
            
            else
                
                ' save time of last trigger found as the endpoint of spike search. 
                ' TODO: Stop at the trigger that closes the LAST bin in the msequence. This probably only matters 
                ' if a repeat>1 is used.
                tLastTrigger := triggers[ntrig%-1];
                
                for ichannel% := 1 to amsNWavemarks% do 
                    tTempTrigger := TriggerTime;
                    for itrig% := 0 to ntrig%-1 do
                        nspikes% := View(amsWindowData%).count(amsWavemarkPorts%[ichannel%-1]+1, tTempTrigger, triggers[itrig%]);
                        BlockData$[ichannel%-1]:=BlockData$[ichannel%-1]+" "+str$(nspikes%);
                        tTempTrigger := triggers[itrig%];
                    next 
                next
                
                TriggerTime := triggers[ntrig%-1];
                BlockIndex += (ntrig%);
                TriggerNumber += (ntrig%);
                
                'PrintLog("Found %d triggers, holding %d\n", ntrig%, BlockIndex);
                'PrintLog("Time %f num %d\n", TriggerTime, TriggerNumber);

                    
                ' Was that the last trigger? 
                if TriggerNumber >= 32768 * amsRepeats% then
                    status% := -1;
                endif
                    
                ' BlockIndex counts the number of terms we've collected and stored in BlockData$[] for each channel. 
                ' I'm just using a high water mark where the string of the first channel is >128 chars
                if status% < 0 or BlockIndex > 100 or Len(BlockData$[0])>128 then
                    'PrintLog("SendData: %d %d(>100) %d(>128)\n", status%, BlockIndex, Len(BlockData$[0]));
                    for ichannel% := 1 to amsNWavemarks% do 
                        SendMessage(BlockData$[ichannel%-1]);
                        BlockData$[ichannel%-1]:=str$(ichannel%);
                    next
                    PercentComplete:=TriggerNumber/(32768*amsRepeats%);  'This line and the following line removed from
                    TimeLeft:=(TriggerTime/PercentComplete)-TriggerTime;  'FOR loop just above because they only need to be done once -jj
                    ToolbarText("M Sequence "+str$(100*PercentComplete)+"% complete. Estimated "+str$(TimeLeft)+" seconds left");
                    BlockIndex:=0;
                endif
            endif
        endif
    endif
    return status%;
end



'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' djs Modified version of SendMessage which waits in a loop until the 'SendMessage' app
' has completed. 

proc SendMessage(Message$)
var hSend%;
    
hSend% := ProgRun(GetBinDir$(1) + "\\sendmessage "+Message$);
    
' NOTE: THIS 'while' STOPS PROCESSING UNTIL THE ACTIVEX SERVER HAS RECEIVED THE DATA. 
' TO REVERT TO THE OLD WAY (DON'T STOP PROCESSING) JUST COMMENT OUT THE LINES FROM 'while' TO 'wend'
' BELOW.
'
' COMMENT LINES BY PUTTING AN APOSTROPHE AT THE BEGINNING OF THE LINE. 
'
' Now we pause and wait until this app finishes....
while ProgStatus(hSend%) = 1 do
	Yield(.1);
wend

end
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


func SendSerialMsgNeuropixel(port%, msg$, term$)
    var buffer$;
    var count% := 60;  'Yikes, it takes a long time to get the Neuropixel stuff ready!
    var status% := 0;
    SerialWrite(port%, msg$, term$);
    while count% > 0 and status% < 1 do
        count% := count% - 1;
        status% := SerialRead(port%, buffer$, term$);
        printlog("read status %d buffer %s\n", status%, buffer$);
        if status% > 0 then
            if not (buffer$ = "ACK") then
                PrintLog("[ERROR]: Failed to receive ACK from imaging acq machine\n");
                message("Received error from Neuropixel listener!  Error is\n" + buffer$);
                status% := 0;
                break;
            endif
        endif
    wend;
    if status% > 0 then
        return 1;
    else
        return 0;
    endif    
end;